developer=https://github.com/shaarli
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.9.0
pkgrel=1
pkgdesc="Your own URL shortener"
arch=('any')
license=("custom:zlib")
source=("https://github.com/shaarli/Shaarli/releases/download/v${pkgver}/shaarli-v${pkgver}-full.tar.gz")
options=('!strip')
sha512sums=('b90e0b9dbb9eb46c67fb57c27695e5c3956f675955040c0a6035e3ab795179e26dd3298f2dcd6661c0d413c723484d6c4e05e33ab0bb82f753cd7f46523981fc')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# License
    mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
    install -m644 ${startdir}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/${pkgname}
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/${pkgname}/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m644 ${startdir}/tmpl/deny-htaccess      ${pkgdir}/usr/share/${pkgname}/tmpl/
    install -m644 ${startdir}/tmpl/datastore.php.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    install -m755 ${startdir}/bin/generate-config.pl ${pkgdir}/usr/share/${pkgname}/bin/
    install -m755 ${startdir}/bin/fix-permissions.pl ${pkgdir}/usr/share/${pkgname}/bin/

    rsync --quiet --archive --exclude '.git*' ${startdir}/src/Shaarli/* ${pkgdir}/usr/share/${pkgname}/web
}
