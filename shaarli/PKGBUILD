developer=https://github.com/shaarli
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.8.0
pkgrel=3
pkgdesc="Your own URL shortener"
arch=('any')
license=("custom:zlib")
source=("https://github.com/shaarli/Shaarli/releases/download/v${pkgver}/shaarli-v${pkgver}-full.tar.gz")
options=('!strip')
sha512sums=('30d4ff2b7cc554d98ce847f75c440889665a46edece808037e85a49d8ab19be22e5324d0175a61276f042077b76f23d7161e9e98fd31eb4d857a8b2ee000b42a')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# License
    mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
    install -m644 ${startdir}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/${pkgname}
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/${pkgname}/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m644 ${startdir}/tmpl/deny-htaccess      ${pkgdir}/usr/share/${pkgname}/tmpl/
    install -m644 ${startdir}/tmpl/datastore.php.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    install -m755 ${startdir}/bin/generate-config.pl ${pkgdir}/usr/share/${pkgname}/bin/
    install -m755 ${startdir}/bin/fix-permissions.pl ${pkgdir}/usr/share/${pkgname}/bin/

    rsync --quiet --archive --exclude '.git*' ${startdir}/src/Shaarli/* ${pkgdir}/usr/share/${pkgname}/web
}
